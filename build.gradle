buildscript {
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.2'
    }
}

plugins {
    id 'java'
    id "com.diffplug.spotless" version "5.0.0"
    id "net.ltgt.errorprone" version "2.0.2"
    id 'signing'
    id 'maven-publish'
    id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
    id 'com.adarshr.test-logger' version '3.2.0'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'jacoco'
apply plugin: 'com.diffplug.spotless'
apply plugin: 'com.google.protobuf'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
    mavenCentral()
}

dependencyLocking {
    lockAllConfigurations()
}

dependencies {
    errorprone "com.google.errorprone:error_prone_core:latest.release"
    errorproneJavac "com.google.errorprone:javac:9+181-r4173-1"

    ['armeria',
     'armeria-brave',
     'armeria-grpc',
     'armeria-jetty9',
     'armeria-kafka',
     'armeria-logback',
     'armeria-retrofit2',
     'armeria-rxjava3',
     'armeria-saml',
     'armeria-thrift0.13',
     'armeria-tomcat9',
     'armeria-zookeeper3'].each {
        implementation "com.linecorp.armeria:${it}:1.14.0"
    }

    implementation "com.github.cliftonlabs:json-simple:2.1.2"
    implementation "org.json:json:20220924"
    implementation 'org.jetbrains:annotations:16.0.2'
    implementation "io.grpc:grpc-netty-shaded:1.40.1"
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.grpcmock:grpcmock-junit5:0.9.3'

    // JUnit annotations
    implementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    implementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'

    // Test Containers
    implementation "org.testcontainers:testcontainers:1.17.5"

    // Logging
    runtimeOnly 'ch.qos.logback:logback-classic:1.2.3'
    runtimeOnly 'org.slf4j:log4j-over-slf4j:1.7.30'

    // Test dependencies are used for testing Filibuster.  Any jUnit, etc. have to be main dependencies
    // with the assumption that Filibuster is only pulled in as a test dependency in the application that
    // uses it.
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    testImplementation 'io.opentelemetry:opentelemetry-api:1.6.0'
    testImplementation 'io.opentelemetry:opentelemetry-extension-annotations:1.6.0'

    testImplementation 'io.opentelemetry.instrumentation:opentelemetry-instrumentation-api:1.6.0-alpha'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    maxParallelForks = 1
    timeout = Duration.ofMinutes(60)
    minHeapSize = "128m" // initial heap size
    maxHeapSize = "2048m" // maximum heap size
    jvmArgs '-XX:MaxPermSize=256m' // mem argument for the test JVM
}

jacocoTestReport {
    dependsOn test
}

tasks.withType(JavaCompile) {
    options.compilerArgs += '-parameters'
    options.compilerArgs += '-Xlint:unchecked'
    options.compilerArgs += '-Xlint:deprecation'
}

tasks.withType(JavaCompile).configureEach {
    options.forkOptions.jvmArgs.addAll(['--add-opens', 'jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED'])

    // stolen from opentelemetry.
    options.errorprone {
        enabled = rootProject.findProperty("disableErrorProne") != "true"
        disableWarningsInGeneratedCode = true
        allDisabledChecksAsWarnings = true

        excludedPaths = ".*/build/generated/.*"

        // Doesn't work well with Java 8
        disable("FutureReturnValueIgnored")

        // Require Guava
        disable("AutoValueImmutableFields")
        disable("StringSplitter")
        disable("ImmutableMemberCollection")

        // Don't currently use this (to indicate a local variable that's mutated) but could
        // consider for future.
        disable("Var")

        // Don't support Android without desugar
        disable("AndroidJdkLibsChecker")
        disable("Java7ApiChecker")
        disable("StaticOrDefaultInterfaceMethod")

        // Great check, but for bytecode manipulation it's too common to separate over
        // onEnter / onExit
        disable("MustBeClosedChecker")

        // Common to avoid an allocation. Revisit if it's worth opt-in suppressing instead of
        // disabling entirely.
        disable("MixedMutabilityReturnType")

        // We end up using obsolete types if a library we're instrumenting uses them.
        disable("JdkObsolete")
        disable("JavaUtilDate")

        // Storing into a variable in onEnter triggers this unfortunately.
        disable("UnusedVariable")

        // some moving.
        disable("DefaultPackage")

        // var usage: potentially need to revert for opentelemetry integration.
        disable("Varifier")

        // wildcard import: potentially need to revert for opentelemetry integration.
        disable("WildcardImport")

        // address this.
        disable("PrivateConstructorForUtilityClass")

        // with older APIs.
        disable("InconsistentOverloads")
        disable("TypeParameterNaming")

        // We don't use tools that recognize.
        disable("InlineMeSuggester")
        disable("DoNotCallSuggester")

        disable("UnnecessarilyFullyQualified")

        if (name.contains("Jmh") || name.contains("Test")) {
            disable("MemberName")
        }
    }
}

protobuf {
    // Configure the protoc executable.
    protoc {
        // Download from the repository.
        artifact = 'com.google.protobuf:protoc:3.12.0'
    }

    // Locate the codegen plugins.
    plugins {
        // Locate a plugin with name 'grpc'.
        grpc {
            // Download from the repository.
            artifact = 'io.grpc:protoc-gen-grpc-java:1.36.1'
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            grpc {}
        }
    }
}

javadoc {
    options {
        links 'https://docs.oracle.com/javase/8/docs/api/'
    }
}

// Publishing
// adapted from: https://selectfrom.dev/publishing-your-first-open-source-library-with-gradle-50bd0b1cd3af

group = "cloud.filibuster"
archivesBaseName = "instrumentation"
version = "1.2.1"

java {
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Filibuster instrumentation for the JVM'
                artifactId = 'instrumentation'
                packaging = 'jar'
                description = 'Instrumtation for the JVM to enable the Filibuster testing utility.'
                url = 'https://github.com/filibuster-testing/filibuster-java-instrumentation'

                scm {
                    connection = 'scm:git:git://github.com/filibuster-testing/filibuster-java-instrumentation.git'
                    developerConnection = 'scm:git:ssh://github.com/filibuster-testing/filibuster-java-instrumentation.git'
                    url = 'https://github.com/filibuster-testing/filibuster-java-instrumentation'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'cmeiklejohn'
                        name = 'Christopher S. Meiklejohn'
                        email = 'christopher.meiklejohn@gmail.com'
                    }
                }
            }
        }
    }
}

signing {
    def signingKeyId = findProperty("signingKeyId")
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

ext.genOutputDir = file("$buildDir/generated-resources")

task generateVersionTxt()  {
    ext.outputFile = file("$genOutputDir/version.txt")
    outputs.file(outputFile)
    doLast {
        outputFile.text = """GroupId: ${project.group}
Name: ${project.name}
Version: $version
Build-time: ${java.time.LocalDateTime.now()}
"""
    }
}

sourceSets.main.output.dir genOutputDir, builtBy: generateVersionTxt

task unitTest(type: Test) {
    useJUnitPlatform()

    filter {
        includeTestsMatching('cloud.filibuster.unit.*')
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform()

    maxParallelForks = 1
    timeout = Duration.ofMinutes(60)
    minHeapSize = "128m" // initial heap size
    maxHeapSize = "2048m" // maximum heap size
    jvmArgs '-XX:MaxPermSize=256m' // mem argument for the test JVM

    filter {
        includeTestsMatching('cloud.filibuster.integration.*')
    }
}

task functionalJavaSmokeTest(type: Test) {
    useJUnitPlatform()

    maxParallelForks = 1
    timeout = Duration.ofMinutes(60)
    minHeapSize = "128m" // initial heap size
    maxHeapSize = "2048m" // maximum heap size
    jvmArgs '-XX:MaxPermSize=256m' // mem argument for the test JVM

    filter {
        includeTestsMatching('cloud.filibuster.functional.java.smoke.*')
    }
}

task functionalJavaHelloTest(type: Test) {
    useJUnitPlatform()

    maxParallelForks = 1
    timeout = Duration.ofMinutes(60)
    minHeapSize = "128m" // initial heap size
    maxHeapSize = "2048m" // maximum heap size
    jvmArgs '-XX:MaxPermSize=256m' // mem argument for the test JVM

    filter {
        includeTestsMatching('cloud.filibuster.functional.java.hello.*')
    }
}

task functionalJavaAnalysisTest(type: Test) {
    useJUnitPlatform()

    filter {
        includeTestsMatching('cloud.filibuster.functional.java.analysis.*')
    }
}

task functionalJavaLatencyTest(type: Test) {
    useJUnitPlatform()

    filter {
        includeTestsMatching('cloud.filibuster.functional.java.latency.*')
    }
}

task functionalJavaCompositionalTest(type: Test) {
    useJUnitPlatform()

    filter {
        includeTestsMatching('cloud.filibuster.functional.java.compositional.*')
    }
}

task functionalPythonTest(type: Test) {
    useJUnitPlatform()

    filter {
        includeTestsMatching('cloud.filibuster.functional.python.*')
    }
}

task functionalDockerTest(type: Test) {
    useJUnitPlatform()

    filter {
        includeTestsMatching('cloud.filibuster.functional.docker.*')
    }
}

task functionalJavaSpecificsTest(type: Test) {
    useJUnitPlatform()

    timeout = Duration.ofMinutes(60)

    filter {
        includeTestsMatching('cloud.filibuster.functional.java.specifics.*')
    }
}